\documentclass[10pt, a4paper,english,spanish,hidelinks]{article}
\usepackage{amsmath}
\usepackage{amsfonts}
\usepackage{amssymb}
\usepackage{caratula}
\usepackage[spanish, activeacute]{babel}
\usepackage[usenames,dvipsnames]{color}
\usepackage[width=15.5cm, left=3cm, top=2.5cm, height= 24.5cm]{geometry}
\usepackage{graphicx}
\usepackage[utf8]{inputenc}
\usepackage{listings}
\usepackage{multicol}
\usepackage{subfig}
\usepackage{float}
\usepackage{color,hyperref}


\usepackage{listings}
\usepackage{babel}
\usepackage{url}
\usepackage{lscape}
\parindent = 15 pt
\parskip = 11 pt

\usepackage{fancyhdr}
\usepackage{hyperref}
\usepackage{amsmath}
\usepackage{amsfonts}
\usepackage{amssymb}
\usepackage[utf8]{inputenc}
\usepackage{graphicx}
\usepackage{caption}
\usepackage{color}
%\usepackage{appendix}
\usepackage{fancyhdr}


\materia{Paradigmas de Lenguajes de Programación}

\titulo{Trabajo Práctico 3}
\fecha{20 de Noviembre de 2014}
\grupo{Grupo tas lokito}
\integrante{Gauder María Lara}{027/10}{marialaraa@gmail.com}
\integrante{Mataloni Alejandro}{706/07}{amataloni@gmail.com}
\integrante{Reartes Marisol}{422/10}{marisol.r5@hotmail.com}


\begin{document}
\pagestyle{myheadings}
\maketitle
\markboth{Paradigmas de Lenguajes de Programación}{Programación Orientada a Objetos}

\section{Código}


\begin{verbatim}
Object subclass: #Jugador
	instanceVariableNames: 'eleccion'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccion

	self subclassResponsibility. 

perdioEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 

empatoEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 
	
setEleccion: aEleccion 
	eleccion := aEleccion. 

ganoEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "

Jugador class
	instanceVariableNames: ''

siempre: unaEleccion
	^ JugadorSiempre new setEleccion: unaEleccion. 

humano
	^ JugadorInteractivo new. 

aleatorioConGenerador: unGenerador
	^ JugadorAleatorio new setGenerador: unGenerador. 

adaptativoIniciandoCon: unaEleccion
	^ JugadorAdaptativo new setEleccion: unaEleccion. 

conHistoriaHasta: cantidadDeResultadosAGuardar
	^ self shouldBeImplemented. 

aleatorio
	^ JugadorAleatorio new setGenerador: Random new. 

Object subclass: #Resultado
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "

jugador1: unJugador conEleccion: unaEleccion jugador2: otroJugador conEleccion: otraEleccion 
	(unaEleccion empataContra: otraEleccion) ifTrue: [
		^ Empate entre: unJugador eligiendo: unaEleccion y: otroJugador eligiendo: otraEleccion.
	].
	^ (unaEleccion ganaContra: otraEleccion)
	ifTrue: [ Victoria de: unJugador eligiendo: unaEleccion contra: otroJugador eligiendo: otraEleccion ]
	ifFalse: [ Victoria de: otroJugador eligiendo: otraEleccion contra: unJugador eligiendo: unaEleccion ]. 

Resultado subclass: #Victoria
	instanceVariableNames: 'ganador eleccionGanadora perdedor eleccionPerdedora'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

ganador
	^ ganador 

ganador: unJugador eleccionGanadora: unaEleccion perdedor: otroJugador eligiendo: otraEleccion 
	ganador := unJugador.
	eleccionGanadora := unaEleccion.
	perdedor := otroJugador.
	eleccionPerdedora := otraEleccion. 

notificarAJugadores
	perdedor perdioEligiendo:  eleccionPerdedora  contra: ganador eligiendo: eleccionGanadora.
	ganador ganoEligiendo: eleccionGanadora contra: perdedor eligiendo: eleccionPerdedora.  

esEmpate
	^ false. 

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "

de: unJugador eligiendo: unaEleccion contra: otroJugador eligiendo: otraEleccion 
	^ self new 
		ganador: unJugador
		eleccionGanadora: unaEleccion
		perdedor: otroJugador
		eligiendo: otraEleccion;
		yourself. 

Resultado subclass: #Empate
	instanceVariableNames: 'jugador1 eleccion1 jugador2 eleccion2'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

jugador1: unJugador eleccion1: unaEleccion jugador2: otroJugador eleccion2: otraEleccion 
	jugador1 := unJugador.
	eleccion1 := unaEleccion.
	jugador2 := otroJugador.
	eleccion2 := otraEleccion. 

notificarAJugadores
	jugador1 empatoEligiendo: eleccion1 contra: jugador2 eligiendo: eleccion2. 
	jugador2 empatoEligiendo: eleccion2 contra: jugador1 eligiendo: eleccion1. 

esEmpate
	^ true. 

ganador
	^ nil. 

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "

entre: unJugador eligiendo: unaEleccion y: otroJugador eligiendo: otraEleccion 
	^ self new
		jugador1: unJugador
		eleccion1: unaEleccion
		jugador2: otroJugador
		eleccion2: otraEleccion;
		yourself. 

Object subclass: #GeneradorRandomParaTest
	instanceVariableNames: 'siguiente'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

siguiente: unNumeroAleatorio
	siguiente := unNumeroAleatorio. 

nextInt: anInteger 
	^ siguiente. 

Object subclass: #Juego
	instanceVariableNames: 'jugador1 jugador2 resultado eleccion1 eleccion2'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

revancha
	^ self iniciar. 

iniciar
	eleccion1 := jugador1 eleccion.
	eleccion2 := jugador2 eleccion.
	resultado := Resultado jugador1: jugador1 conEleccion: eleccion1
				 			jugador2: jugador2 conEleccion: eleccion2.
	resultado notificarAJugadores. 

jugador1: unJugador jugador2: otroJugador 
	jugador1 := unJugador.
	jugador2 := otroJugador. 

resultado
	resultado isNil ifTrue: [ self iniciar ].
	^ resultado. 

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "

mejorDe: cantidadDeJuegos entre: unJugador y: otroJugador
	| j |
	j := Juego entre: unJugador y: otroJugador.
	cantidadDeJuegos timesRepeat: [ j iniciar ]. 

entre: unJugador y: otroJugador 
	^ self new jugador1: unJugador jugador2: otroJugador;
			yourself. 

mejorDe: cantidadDeJuegos contra: unJugador
	Juego mejorDe: cantidadDeJuegos entre: Jugador humano y: unJugador. 

Jugador subclass: #JugadorAdaptativo
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

empatoEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 
	eleccion := eleccionContraria eleccionSuperadora.  

perdioEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 
	eleccion := eleccionContraria eleccionSuperadora.  

eleccion
	^ eleccion. 

Jugador subclass: #JugadorSiempre
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccion
	^ eleccion. 

Object subclass: #Eleccion
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

ganadorContra: otraEleccion
	^ self subclassResponsibility. 

ganadorContraTijera
	^ self subclassResponsibility. 

ganadorContraPapel
	^ Eleccion papel. 

ganadorContraPiedra
	^ self subclassResponsibility. 

noEmpataContra: otraEleccion
	^ (self empataContra: otraEleccion) not. 

empataContra: otraEleccion
	^ (self ganadorContra: otraEleccion) isNil. 

pierdeContra: otraEleccion
	^ (self noEmpataContra: otraEleccion)
		and:
		[ (self ganaContra: otraEleccion) not ] 

ganaContra: otraEleccion
	^ (self noEmpataContra: otraEleccion)
	 	and:
		[ (self ganadorContra: otraEleccion) = self ] 

asString
	^ self class name. 

= otraEleccion
	^self class = otraEleccion class. 

hash
	^ self class hash. 

ganadorContraLagarto
	^ self subclassResponsibility. 

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "
lagarto
	^ Lagarto new. 

papel
	^ Papel new. 

piedra
	^ Piedra new. 

spock
	^ Spock new. 

tijera
	^ Tijera new. 

todas
	^ (Eleccion class selectorsInProtocol: 'creacion') collect: [:a |  Eleccion perform: a]. 

Eleccion subclass: #Tijera
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccionSuperadora
	^ Eleccion piedra. 

ganadorContraTijera
	^nil. 

ganadorContraPapel
	^self. 

ganadorContraPiedra
	^ Eleccion piedra. 

ganadorContra: otraEleccion
	^ otraEleccion ganadorContraTijera . 

Eleccion subclass: #Spock
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccionSuperadora
	^ Eleccion lagarto. 

ganadorContraLagarto
	^ Eleccion lagarto. 

ganadorContra: aEleccion 
	^ aEleccion ganadorContraSpock. 

ganadorContraPiedra
	^ self. 

Eleccion subclass: #Lagarto
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

ganadorContra: otraEleccion
	^ otraEleccion ganadorContraLagarto. 

ganadorContraSpock
	^ self. 

eleccionSuperadora
	^ Eleccion piedra. 

ganadorContraLagarto
	^ nil. 

ganadorContraPiedra
	^ Eleccion piedra. 

Jugador subclass: #JugadorInteractivo
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

perdioEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria 
     UITheme current
     messageIn: Morph new
     text: 'Perdiste contra ', eleccionContraria asString
     title: 'Resultado'. 

empatoEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria
    UITheme current
     messageIn: Morph new
     text: 'Empataste con ', eleccionContraria asString
     title: 'Resultado'.

eleccion
	^ UITheme current
	   chooseDropListIn: Morph new
	   text: 'Que jugamos?'
         title: 'Nueva'
         list: Eleccion todas. 

ganoEligiendo: miEleccion contra: otroJugador eligiendo: eleccionContraria
     UITheme current
     messageIn: Morph new
     text: 'Le ganaste a ', eleccionContraria asString
     title: 'Resultado'. 

Eleccion subclass: #Papel
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccionSuperadora
	^ Eleccion tijera. 

ganadorContraTijera
	^ Eleccion tijera. 

ganadorContraPapel
	^nil. 

ganadorContraPiedra
	^self. 

ganadorContra: otraEleccion
	^otraEleccion  ganadorContraPapel.  

Eleccion subclass: #Piedra
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

eleccionSuperadora
	^ Eleccion papel. 

ganadorContraTijera
	^self. 

ganadorContraPapel
	^ Eleccion papel. 

ganadorContraPiedra
	^nil. 

ganadorContra: otraEleccion
	^otraEleccion ganadorContraPiedra. 

Jugador subclass: #JugadorAleatorio
	instanceVariableNames: 'generador'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PLP-TP3'

setGenerador: unGenerador
	generador := unGenerador. 

eleccion
	|todasLasElecciones|
	todasLasElecciones := Eleccion todas.
	^ todasLasElecciones at: (generador nextInt: todasLasElecciones size). 

\end{verbatim}


\end{document}
